// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    //// Replace this comment with your code.
    // Notice how we started at the deepest if statement, and propogated out
    
    // We only increment whatever value is in the register
    Inc16(in=regOut, out=plusOne);

    // First we check if output should remain the same or incremented
    Mux16(a=regOut, b=plusOne, sel=inc, out=incOut);
    
    // Now we decide whether to load a new value or keep the old one
    Mux16(a=incOut, b=in, sel=load, out=loadOut);
    
    // Only at the end do we check if we just wanna annilihate calculations and keep 0
    Mux16(a=loadOut, b=false, sel=reset, out=calcResult);
    
    // We need something to store our current location in the program
    Register(in=calcResult, load=true, out=out, out=regOut);
}