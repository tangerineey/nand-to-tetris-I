// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.

    // Handles zx and nx.
    Mux16(a=x, b=false, sel=zx, out=newX);
    Not16(in=newX, out=notX);
    Mux16(a=newX, b=notX, sel=nx, out=finalX);

    // Handles zy and ny.
    Mux16(a=y, b=false, sel=zy, out=newY);
    Not16(in=newY, out=notY);
    Mux16(a=newY, b=notY, sel=ny, out=finalY);

    // Handles f.
    Add16(a=finalX, b=finalY, out=sum);
    And16(a=finalX, b=finalY, out=conjunction);
    Mux16(a=conjunction, b=sum, sel=f, out=result);

    // Handles no and ng.
    // ng is just the most significant bit by 2's complement.
    Not16(in=result, out=notResult);
    Mux16(a=result, b=notResult, sel=no, out=out, out[0..7]=half1, out[8..15]=half2, out[15]=ng); // Needed for the Or8Way
    
    // Handles zr. 
    // Explanation: To check if some bits are zero,
    // we simply or every bit and check the result.
    // If it is not zero the result will be 1 and if 
    // not that it will be zr.
    Or8Way(in=half1, out=isZero1);
    Or8Way(in=half2, out=isZero2);
    Or(a=isZero1, b=isZero2, out=isZero);
    Not(in=isZero, out=zr);
}